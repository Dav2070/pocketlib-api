(# Get the latest authors)
(var author_objects (func get_latest_author_table_objects ()))
(var authors (list))

(for author_object in author_objects (
	(# Check if the author has uploaded a profile image)
	(if (! (is_nil author_object.properties.profile_image)) (
		(# Convert the table object to hash)
		(var author (hash
			(uuid author_object.uuid)
			(first_name author_object.properties.first_name)
			(last_name author_object.properties.last_name)
			(website_url author_object.properties.website_url)
			(facebook_url (func build_facebook_url (author_object.properties.facebook_username)))
			(instagram_url (func build_instagram_url (author_object.properties.instagram_username)))
			(twitter_url (func build_twitter_url (author_object.properties.twitter_username)))
			(profile_image true)
			(profile_image_blurhash author_object.properties.profile_image_blurhash)
		))

		(authors.push author)
	))
))

(# Render the result)
(render_json (hash
	(authors authors.reverse)
) 200)