(# Get the jwt)
(var jwt (get_header Authorization))
(func render_validation_errors ((list
	(func validate_jwt_missing (jwt))
)))

(# Decode the jwt)
(var decoded_jwt (func decode_jwt (jwt)))

(# Validate the dev)
(func render_validation_errors ((list
	(func validate_dev_is_pocketlib_dev (decoded_jwt.dev_id))
)))

(# Check if the user is an admin)
(var is_admin (env.ADMINS.contains decoded_jwt.user_id))

(# Get the store book)
(var store_book (func get_store_book_table_object (uuid)))

(if (is_nil store_book) (
	(# Render Resource does not exist: StoreBook)
	(func render_validation_errors ((list (hash (error (get_error 2805)) (status 404)))))
))

(# Get the status)
(var status (if (is_nil store_book.properties.status) "unpublished" else store_book.properties.status))

(# Check if the user can access the store book cover)
(if (!(
	(is_admin or (status == "published")) 
	or (
		(# Get the collection of the store book)
		(var collection (func get_store_book_collection_table_object (store_book.properties.collection)))

		(# Get the author of the collection)
		(var author (func get_author_table_object (collection.properties.author)))

		(decoded_jwt.user_id == author.user_id)
	))) (
		(# Access not allowed)
		(func render_validation_errors ((list (hash (error (get_error 1102)) (status 403)))))
	)
)

(# Define function for rendering no cover error)
(def render_no_cover () (
	(func render_validation_errors ((list (hash (error (get_error 2806)) (status 404)))))
))

(# Check if the store book has a cover)
(if (is_nil store_book.properties.cover) (
	(func render_no_cover ())
))

(# Get the cover)
(var cover (TableObject.get store_book.properties.cover))

(if (is_nil cover) (
	(func render_no_cover ())
))

(var cover_file (TableObject.get_file store_book.properties.cover))

(# Return the content of the file)
(render_file cover_file cover.properties.type (store_book.properties.title + "." + cover.properties.ext) 200)